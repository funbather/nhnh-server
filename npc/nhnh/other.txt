prt_monk,216,164,4	script	Stash#stash	TREASURECHEST,{
	openstorage();
	end;
}

function	script	doWarpParty	{
	if(getpartyleader(getcharid(1),2) == getcharid(0)) {
		getmapxy(.@mapl$, .@xl, .@yl, 0);
		warpparty("Leader", .@xl, .@yl, getcharid(1), .@mapl$);
	}
	return;
}

function	script	useCoinBag	{
	.@zenygain = rand(25,250);
	Zeny += .@zenygain;
	dispbottom(sprintf("Gained %d zeny!",.@zenygain));
	
	if( rand(100) < 25 ) {
		.@itemroll = rand(10000);
		
		if     ( .@itemroll >= 7000 ) getitem(_mythril, 1);
		else if( .@itemroll >= 4000 ) getitem(_orichalcum, 1);
		else if( .@itemroll >= 1700 ) getitem(_emptybijou, 1);
		else if( .@itemroll >=  700 ) getitem(_arcanebijou, 1);
		else if( .@itemroll >=  450 ) getitem(_blessedbijou, 1);
		else if( .@itemroll >=  250 ) getitem(_divinehammer, 1);
		else if( .@itemroll >=  100 ) getitem(_capriciousbijou, 1);
		else                          getitem(_discordantbijou, 1);
	}
	
	return;
}

function	script	useWarpstone	{	
	getmapxy(.@map$, .@xx, .@yy, 0);
	
	if( has_instance2(.@map$) >= 0 ) {
		instance_attach(has_instance2(.@map$));
		getmapxy('map$, 'xx, 'yy, 0);
		
		if( 'map$ != "" && 'portalname$ != "" ) {
			enablenpc('portalname$);
			movenpc('portalname$, 'xx, 'yy);
		}
	}
	return;
}

function	script	giveTokens	{	
	if( getcharid(CHAR_ID_PARTY) == 0 ) {
		getitem(_token, 1);
	} else {
		getmapxy(.@map$, .@xx, .@yy, 0);
	
		getpartymember(getcharid(CHAR_ID_PARTY), 2);
		
		.@count = $@partymembercount;
		copyarray(.@partyaid[0], $@partymemberaid[0], $@partymembercount);
		
		for( .@i = 0; .@i < .@count; .@i++ ) {
			getmapxy(.@map2$, .@xx, .@yy, 0, .@partyaid[.@i]);
			
			if( .@map$ == .@map2$ ) {
				getitem(_token, 1, .@partyaid[.@i]);
			}
		}
	}

	return;
}