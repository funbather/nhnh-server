prt_monk,202,172,6	script	Ysolt#blacksmith	4_F_FRUIT,{

	disable_items;
	mes("[Ysolt]");
	mes("Hey, kid. Blacksmith at your service.");
	menu("Upgrade an item!",menuUpgrade,"What are your services?",askServices,"Nothing today.",thatsAll);
	
backToMenu:

	cleartext;
	mes("[Ysolt]");
	mes("Done thinkin' it over?");
	menu("Upgrade an item!",menuUpgrade,"What are your services?",askServices,"That's all.",thatsAll);
	
askServices:

	cleartext;
	mes("[Ysolt]");
	mes("If you got the right stuff, I can fix up an old or worn-out piece of equipment back into fighting shape.");
	next;
	mes("[Ysolt]");
	mes("With ^5555AA^boMythril Ore^ld^000000 I can increase an item's quality by ^5555AA^bo10%^ld^000000, but only up to a max of 100%.");
	mes("\nAn item's quality directly affects its base stats, a blade at 80% quality is only 80% as good as one at 100%.");
	next;
	mes("[Ysolt]");
	mes("If you got ^5555AA^boOrichalcum Ore^ld^000000, I can increase an item's level by ^5555AA^bo5^ld^000000, but only up to your own base level.");
	mes("\nItem level determines an item's base stats, and has a much greater influence than quality. Not keeping your gear levels in line with your own can be a fatal mistake.");
	next;
	mes("[Ysolt]");
	mes("Finally, if you can manage to get your hands on a ^5555AA^boDivine Hammer^ld^000000, I can transform a lower ranked item into one of a higher rank.");
	next;
	goto backToMenu;
	
menuUpgrade:

	cleartext;
	mes("[Ysolt]");
	mes("Let's get started, then. What've you got?");
	menu("Mythril Ore (+10% Quality)",upgQuality,"Orichalcum Ore (+5 Item Level)",upgLevel,"Divine Hammer (+ Rank)",upgType,"I've changed my mind.",thatsAll);	

upgQuality:

	cleartext;
	setarray(.@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2");
	.@menu$ = "";
	for( .@i = 1; .@i <= 9; .@i++ ) {
		if( getequipisequiped(.@i+1) ) {
			.@menu$ += getequipname(.@i+1) + " - " + getequiprefinerycnt(.@i+1) + "%";
			.@equipped = 1;
		}
		.@menu$ += ":";
	}
	
	if( !.@equipped ) {
		cleartext;
		mes("[Ysolt]");
		mes("You're not even wearin'... ugh, nevermind. Don't waste my time, kid.");
		close;
	}
	
	.@part = select(.@menu$) + 1;
	
	if( getequiprefinerycnt(.@part) == 100 ) {
		cleartext;
		mes("[Ysolt]");
		mes("I got my limits, kid. I can't make this beauty any better.");
		close;
	}
	
	if( getequiprefinerycnt(.@part) > 100 ) {
		cleartext;
		mes("[Ysolt]");
		mes("This is a masterpiece, kid! I couldn't make it better if I tried.");
		mes("\nYou're on your own on this one.");
		close;
	}
	
	.@material = 800;

	if( !countitem(.@material) ) {
		cleartext;
		mes("[Ysolt]");
		mes("I need some Mythril Ore to actually do this, kid.");
		close;
	}
	
	delitem(.@material,1);
	successrefitem(.@part,10,100);
	
	cleartext;
	mes("[Ysolt]");
	mes("Success! Enjoy, kid.");
	close;

upgLevel:

	cleartext;
	setarray(.@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2");
	.@menu$ = "";
	for( .@i = 1; .@i <= 9; .@i++ ) {
		if( getequipisequiped(.@i+1) ) {
			.@menu$ += getequipname(.@i+1) + " - Lv. " + getequipilvl(.@i+1);
			.@equipped = 1;
		}
		.@menu$ += ":";
	}
	
	if( !.@equipped ) {
		cleartext;
		mes("[Ysolt]");
		mes("You're not even wearin'... ugh, nevermind. Don't waste my time, kid.");
		close;
	}
	
	.@part = select(.@menu$) + 1;
	
	if( getequipilvl(.@part) >= BaseLevel ) {
		mes("[Ysolt]");
		mes("I can't make it any better than this right now, kid.");
		close;
	}
	
	.@material = 801;
	
	if( !countitem(.@material) ) {
		cleartext;
		mes("[Ysolt]");
		mes("I need some Orichalcum Ore to actually do this, kid.");
		close;
	}
	
	delitem(.@material,1);
	successrefitemilvl(.@part,5,BaseLevel);
	
	cleartext;	
	mes("[Ysolt]");
	mes("Success! Enjoy, kid.");
	close;
	
upgType:

	cleartext;
	setarray(.@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2");
	.@menu$ = "";
	for( .@i = 1; .@i <= 9; .@i++ ) {
		if( getequipisequiped(.@i+1) ) {
			.@menu$ += getequipname(.@i+1);
			.@equipped = 1;
		}
		.@menu$ += ":";
	}
	
	if( !.@equipped ) {
		cleartext;
		mes("[Ysolt]");
		mes("You're not even wearin'... ugh nevermind. Don't waste my time, kid.");
		close;
	}
	
	.@part = select(.@menu$) + 1;
	.@upg = -1;
	.@itid = getequipid(.@part);
	
	switch( .@itid ) {
		case 1:
		case 2:
		case 4:
		case 5:
		case 7:
		case 8:
		case 10:
		case 11:
		case 13:
		case 14:
		case 16:
		case 17:
		case 19:
		case 20:
		case 25:
		case 26:
		case 28:
		case 29:
		case 31:
		case 32:
		case 34:
		case 35:
		case 100:
		case 101:
		case 103:
		case 104:
		case 106:
		case 107:
		case 109:
		case 110:
		case 112:
		case 113:
		case 115:
		case 116:
		case 118:
		case 119:
			.@upg = .@itid + 1;
			break;
	}

	
	if( .@upg == -1 ) {
		mes("[Ysolt]");
		mes("Kid, this is already the best of its kind. Nothing to do here.");
		close;
	}
	
	.@material = 808;
	
	cleartext;
	if( !countitem(.@material) ) {
		mes("[Ysolt]");
		mes("I need a Divine Hammer to do this, kid.");
		close;
	}
	
	delitem(.@material,1);
	
	.@oldtype = getequipid(.@part);
	.@qual    = getequiprefinerycnt(.@part);
	.@ilvl    = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);
	
	.@roll1 = getequiprolls(.@part,0);
	.@roll2 = getequiprolls(.@part,1);
	.@roll3 = getequiprolls(.@part,2);
	.@roll4 = getequiprolls(.@part,3);
	
	unequip(.@part);
	delitem2(.@oldtype,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);
	
	autoequip(.@upg,1);
	spawnitem(.@upg,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@upg,0);
	
	specialeffect2 154;
	
	mes("[Ysolt]");
	mes("Success! Enjoy, kid.");
	close;
	
thatsAll:
	cleartext;
	mes("[Ysolt]");
	mes("See you around, kid.");
	close;
}