-	script	DropSystem	-1,{
  OnNPCKillEvent:
		callfunc("RollForItem",100,getmonsterinfo(killedrid,1),0);
		
		if(extraitem) // chance to roll for an additional item
			callfunc("RollForItem",extraitem,getmonsterinfo(killedrid,1),0);

		if(rand(1,10000) <= 300) // Mythril 3%
			spawnitem _mythril,1,0,0,0,0,0,0,0,0,0,0,0;
		if(rand(1,10000) <= 300) // Orichalcum 3%
			spawnitem _orichalchum,1,0,0,0,0,0,0,0,0,0,0,0;
		if(rand(1,10000) <= 25)  // Divine Hammer .25%
			spawnitem _divinehammer,1,0,0,0,0,0,0,0,0,0,0,0;
		
		if(rand(1,10000) <= 200) // Empty 2%
			spawnitem _emptybijou,1,0,0,0,0,0,0,0,0,0,0,0;
		if(rand(1,10000) <= 100) // Arcane 1%
			spawnitem _arcanebijou,1,0,0,0,0,0,0,0,0,0,0,0;	
		if(rand(1,10000) <= 25)  // Blessed 0.25%
			spawnitem _blessedbijou,1,0,0,0,0,0,0,0,0,0,0,0;
		if(rand(1,10000) <= 20)  // Capricious 0.2%
			spawnitem _capriciousbijou,1,0,0,0,0,0,0,0,0,0,0,0;
		if(rand(1,10000) <= 10)  // Discordant 0.1%
			spawnitem _discordantbijou,1,0,0,0,0,0,0,0,0,0,0,0;
}

function	script	RollForItem	{
    .@dropchance = getarg(0);
    .@moblvl = getarg(1);
    .@SHARDMIN = 500;
    .@SHARDMAX = 518;
    .@SEALMIN = 600;
    .@SEALMAX = 612;
    
    setarray .@rank1[0],1,4,7,10,13,16,19,25,28,31,34,100,103,106,109,112,115,118;
    setarray .@rank2[0],2,5,8,11,14,17,20,26,29,32,35,101,104,107,110,113,116,119;
    setarray .@rank3[0],3,6,9,12,15,18,21,22,23,24,37,38,39,40,41,42,43,44,45,46,27,30,33,36,102,105,108,111,114,117,120;
    setarray .@uniques[0],1;
    
    .@iqty = 100 + readparam(bLuk2); // Inc Item Quantity
    
    // Do we get a drop at all?
    if((.@dropchance * .@iqty) > rand(1,10000)) {
      .@random = rand(1,10000);
      
      if(.@random <= 6000) {
				.@rnd = rand(getarraysize(.@rank1));  // 60% For Tier 1
				.@itid = .@rank1[.@rnd];
      } else if (.@random <= 9000) {
				.@rnd = rand(getarraysize(.@rank2));  // 30% For Tier 2
				.@itid = .@rank2[.@rnd];
      } else {
				.@rnd = rand(getarraysize(.@rank3));  // 10% For Tier 3
				.@itid = .@rank3[.@rnd];
      }
      
      // Random Bonuses
      // Base
      //  5%    3 + Seal
      //  10%   3
      //  15%   2
      //  30%   1
      //  40%   0
      
      .@random = rand(1,10000);
      
      if(.@random <= 4000) {
        // Nothing!
      } else if (.@random <= 7000) {
				.@socket1 = rand(.@SHARDMIN,.@SHARDMAX);
      } else if (.@random <= 8500) {
				.@socket1 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket2 = rand(.@SHARDMIN,.@SHARDMAX);
      } else if (.@random <= 9500) {
				.@socket1 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket2 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket3 = rand(.@SHARDMIN,.@SHARDMAX);
      } else {
				.@socket1 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket2 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket3 = rand(.@SHARDMIN,.@SHARDMAX);
				.@socket4 = rand(.@SEALMIN,.@SEALMAX);
      }

      .@ilvl = .@moblvl; // Item Level equal to mob's level for now...
      .@random = rand(1,10000);
      
      // Quality Roll
      // 80% Chance 65~100
      // 15% Chance 101~115
      // 5%  Chance 116~125
      
      if(.@random <= 8000)
				.@quality = rand(65,100);
			else if(.@random <= 9500)
				.@quality = rand(101,115);
			else
				.@quality = rand(116,125);
      
      .@roll1 = rand(BaseLevel,49+BaseLevel);
      .@roll2 = rand(BaseLevel,49+BaseLevel);
      .@roll3 = rand(BaseLevel,49+BaseLevel);
      .@roll4 = rand(BaseLevel,49+BaseLevel); // Enchantment roll range tied to player Base Level
       
      // Finally, generate the item!
      // getarg(2) flags
      // 0 - Drop on the ground (monster kills, etc)
      // 1 - Give to player
			spawnitem .@itid,1,.@quality,.@ilvl,getarg(2),.@socket1,.@socket2,.@socket3,.@socket4,.@roll1,.@roll2,.@roll3,.@roll4;
    }
    
    return 1;
}