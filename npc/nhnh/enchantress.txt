prt_monk,199,169,6	script	Serra#enchantress	4_F_ALCHE_A,{
	disable_items;

  .@SHARDMIN = 500;
  .@SHARDMAX = 534;
  .@SEALMIN = 600;
  .@SEALMAX = 620;
	
	mes("[Serra]");
	mes("Welcome! Need some enchanting done?");
	menu("I need some enchanting done!",menuEnchant,"Enchanting?",askServices,"Nothing today.",thatsAll);
	cleartext;
	
backToMenu:

	cleartext;
	mes("[Serra]");
	mes("So, what's up?");
	menu("I need some enchanting done!",menuEnchant,"Enchanting?",askServices,"That's all.",thatsAll);
	
askServices:

	cleartext;
	mes("[Serra]");
	mes("Equipment on its own is alright... but what if you could make a piece of equipment way stronger? Way better? Way cooler??");
	mes("\nThat's where I come in!!\nSerra the Enchantress, at your service!");
	next;
	mes("[Serra]");
	mes("Using different ^5555AA^boBijous^ld^000000, I can add enchantments called ^5555AA^boShards^ld^000000 and ^5555AA^boSeals^ld^000000 to equipment.");
	mes("\nThese enchantments grant all sorts of bonuses, and can even do stuff beyond just boosting some stats!");
	next;
	mes("[Serra]");
	mes("\"Wow! What's a bijou?\" you may ask!");
	mes("\nWell, they're magic-laced jewels carried by monsters. They go crazy over 'em! But then again... so do I!!");
	next;
	mes("[Serra]");
	mes("The most common kinds you'll find are ^5555AA^boArcane Bijous^ld^000000 and ^5555AA^boEmpty Bijous^ld^000000.");
	mes("\nArcane Bijous let me add new shards to gear (which is cool!), but Empty Bijous are where things really get exciting!!");
	next;
	mes("[Serra]");
	mes("I can use them to rip out a shard you like from a piece of equipment you may not like, and store it for you. I can then use it to replace a shard in any other piece of equipment you want!");
	mes("\nYou can totally mix and match bonuses this way, and make the stat build of your dreams!!");
	next;
	mes("[Serra]");
	mes("Beyond that, there are rarer bijous out there that can do even more stuff!");
	mes("\nBring me one and I'll see what I can do with it, yeah?");
	next;
	goto backToMenu;
	
menuEnchant:

	cleartext;
	mes("[Serra]");
	mes("Yeah? What kind?");
	menu("Enscribe Empty Bijou.",enchEmpty,"Enchant Item.",enchBijou,"I've changed my mind.",thatsAll);
	cleartext;

enchEmpty:

	cleartext;
	if( !countitem(802) ) {
		mes("[Serra]");
		mes("You know you need an Empty Bijou for this, right?");
		close;
	}
	
	setarray(.@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2");
	.@menu$ = "";
	
	for( .@i = 1; .@i <= 9; .@i++) {
		if( getequipisequiped(.@i+1) ) {
			.@menu$ += getequipname(.@i+1);
			.@equipped = 1;
		}
		.@menu$ += ":";
	}
	
	if ( !.@equipped ) {
		cleartext;
		mes("[Serra]");
		mes("Uh... you actually need some equipment if we're gonna do this...");
		close;
	}
	
	cleartext;
	mes("[Serra]");
	mes("Let's get to it!\n\nRemember, taking a shard from an item will ^FF0000^boirreversably destroy it^ld^000000! Be sure it's nothing you're gonna miss, yeah?");
	next;
		
	mes("[Serra]");
	mes("Which item are we lookin' at?");
	.@part = select(.@menu$)+1;

	if( !getequipisequiped(.@part) ) {
		cleartext;	
		mes("[Serra]");
		mes("Uh... you actually need some equipment if we're gonna do this...");
		close;
	}
	
	if( !getequipcardcnt(.@part) ) {
		cleartext;
		mes("[Serra]");
		mes("I can't make something from nothing, pal.\n\nBring me something enchanted!");
		close;
	}
	
	if( getequipcardcnt(.@part) == 4 ) {
		cleartext;	
		mes("[Serra]");
		mes("Are you sure you want to use this item...? It's overflowing with magic energy!");
		menu("On second thought...",-,"Yeah, I'm sure.",enchEmpty2);
		cleartext;	
		mes("[Serra]");
		mes("Phew, okay. I'd hate to have to destroy something like that!");
		close;
	}
	
	if( getequipid(.@part) >= 200 && getequipid(.@part) < 500 ) { // unique
		cleartext;	
		mes("[Serra]");
		mes("Are you sure you want to use this item...? It looks pretty rare!");
		menu("On second thought...",-,"Yeah, I'm sure.",enchEmpty2);
		cleartext;	
		mes("[Serra]");
		mes("Phew, okay. I'd hate to have to destroy something like that!");
		close;
	}

enchEmpty2:

	cleartext;
	mes("[Serra]");
	mes("Gotcha. So which of these am I ripping out?");
	
	.@menu2$ = "";
	for( .@i = 0; .@i <= 2; .@i++ )
		.@menu2$ += ( (getequipcardid(.@part,.@i)) ? getitemname(getequipcardid(.@part,.@i)) : "" ) + ":";
	.@slot = select(.@menu2$) - 1;
	
	.@ench = getequipcardid(.@part,.@slot);
	.@roll = getequiprolls(.@part,.@slot);

	delitem(802, 1);
	delequip(.@part);
	
	spawnitem(803,1,0,0,1,.@ench,0,0,0,.@roll,0,0,0);
	
	cleartext;
	mes("[Serra]");
	mes("Tada! Simple as that!\n\nSorry about your stuff, but uh... look at the bright side! You got a cool new Fervent Bijou now!");
	close;
	
enchBijou:

	cleartext;
	mes("[Serra]");
	mes("Let's get to it! What're we working with?");
	
	getinventorylist;
	
	.@menu$ = "";
	for( .@i = 0; .@i < @inventorylist_count; .@i++ ) {
		if (@inventorylist_id[.@i] == 803 ||
				@inventorylist_id[.@i] == 804 ||
				@inventorylist_id[.@i] == 805 ||
				@inventorylist_id[.@i] == 806 ||
				@inventorylist_id[.@i] == 807 ) {
			.@menu$ += ( @inventorylist_id[.@i] == 803 ? getitemname(@inventorylist_id[.@i]) + " ["+getitemname(@inventorylist_card1[.@i])+"]" : getitemname(@inventorylist_id[.@i]) );
			.@havebijou = 1;
		}
		.@menu$ += ":";
	}
	
	if( !.@havebijou ) {
		cleartext;
		mes("[Serra]");
		mes("I appreciate the enthusiasm, but I can't do my thing if you don't got the materials.\n\nBring me some bijous and then we'll talk, yeah?");
		close;
	}
	
	.@selection = select(.@menu$) - 1;
	.@bijou     = @inventorylist_id[.@selection];
	.@roll      = @inventorylist_rolls[.@selection];
	.@shard     = @inventorylist_card1[.@selection];
	
	cleartext;
	mes("[Serra]");
	mes("Which item are we lookin' at?");
	setarray(.@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2");
	
	.@menu2$ = "";
	for( .@i = 1; .@i <= 9; .@i++ ) {
		if( getequipisequiped(.@i+1) ) {
			.@menu2$ += getequipname(.@i+1);
			.@equipped = 1;
		}
		.@menu2$ += ":";
	}
	
	if( !.@equipped ) {
		cleartext;	
		mes("[Serra]");
		mes("Uh... you actually need some equipment if we're gonna do this...");
		close;
	}
	.@part = select(.@menu2$)+1;
	
	switch( .@bijou ) {
		case 804: goto enchArcane; break;
		case 805: goto enchBlessed; break;
		case 806: goto enchCapricious; break;
		case 807: goto enchDiscordant; break;
	}
	
	/* FALLTHROUGH IF FERVENT */
	
enchFervent:

	cleartext;
	if( !getequipcardcnt(.@part) ) {
		mes("[Serra]");
		mes("Hey! I can only replace shards with these, not add them!\n\nYou gotta me something already enchanted to work on.");
		close;
	}

	mes("[Serra]");
	mes("Which one of these do you want to replace?");
	.@menu3$ = "";
	for( .@i = 0; .@i <= 2; .@i++ )
		.@menu3$ += ( (getequipcardid(.@part,.@i)) ? getitemname(getequipcardid(.@part,.@i)) : "") + ":";
	.@selection = select(.@menu3$) - 1;
	
	.@ench  = getequipcardid(.@part,.@selection);
	.@type  = getequipid(.@part);
	.@qual  = getequiprefinerycnt(.@part);
	.@ilvl  = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);
	
	.@roll1 = getequiprolls(.@part,0);
	.@roll2 = getequiprolls(.@part,1);
	.@roll3 = getequiprolls(.@part,2);
	.@roll4 = getequiprolls(.@part,3);
	
	unequip(.@part);
	delitem2(.@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);	
	delitem2(.@bijou,1,1,0,0,.@shard,0,0,0);
	
	switch( .@selection ) {
		case 0: 
			.@ench1 = .@shard;
			.@roll1 = .@roll;
		break;
		
		case 1: 
			.@ench2 = .@shard;
			.@roll2 = .@roll;
		break;
		
		case 2: 
			.@ench3 = .@shard;
			.@roll3 = .@roll;
		break;
	}
	
	
	autoequip(.@type,1);
	spawnitem(.@type,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@type,0);

	cleartext;
	mes("[Serra]");
	mes("Tada! Simple as that! Enjoy your stuff!");
	close;
	
enchArcane:

	cleartext;
	if( getequipcardcnt(.@part) >= 3 ) {
		mes("[Serra]");
		mes("Ah, this item already has the maximum number of shards in it, I can't add any more with one of these.\n\nBring me something less... enchanted, yeah?");
		close;
	}
	
	.@numEnch = getequipcardcnt(.@part);
	.@type = getequipid(.@part);
	.@qual = getequiprefinerycnt(.@part);
	.@ilvl = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);
	
	.@roll1 = getequiprolls(.@part,0);
	.@roll2 = getequiprolls(.@part,1);
	.@roll3 = getequiprolls(.@part,2);
	.@roll4 = getequiprolls(.@part,3);
	
	.@rand = (.@ilvl / 2) + rand(24 + .@ilvl);
	
	unequip(.@part);
	delitem2(.@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);	
	delitem(.@bijou,1);
	
	switch(.@numEnch) {
		case 0: 
			.@ench1 = rand(.@SHARDMIN,.@SHARDMAX);
			.@roll1 = .@rand;
		break;
		
		case 1: 
			.@ench2 = rand(.@SHARDMIN,.@SHARDMAX);
			.@roll2 = .@rand;
		break;
		
		case 2: 
			.@ench3 = rand(.@SHARDMIN,.@SHARDMAX); 
			.@roll3 = .@rand;
		break;
	}
	
	autoequip(.@type,1);
	spawnitem(.@type,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@type,0);

	cleartext;
	mes("[Serra]");
	mes("Tada! Simple as that! Enjoy your stuff!");
	close;
	
enchBlessed:

	cleartext;
	if( getequipcardcnt(.@part) < 3 ) {
		mes("[Serra]");
		mes("An item needs three shards already in it to handle the power of a seal... this one's not quite ready.\n\nWe'll try again when it's more enchanted, yeah?");
		close;
	}
	
	.@type = getequipid(.@part);
	.@qual = getequiprefinerycnt(.@part);
	.@ilvl = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);
	
	.@roll1 = getequiprolls(.@part,0);
	.@roll2 = getequiprolls(.@part,1);
	.@roll3 = getequiprolls(.@part,2);
	.@roll4 = getequiprolls(.@part,3);

	unequip(.@part);
	delitem2(.@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);	
	delitem(.@bijou,1);

	.@ench4 = rand(.@SEALMIN,.@SEALMAX);
	.@rand = (.@ilvl / 2) + rand(24 + .@ilvl);
	.@roll4 = .@rand;

	autoequip(.@type,1);
	spawnitem(.@type,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@type,0);

	cleartext;	
	mes("[Serra]");
	mes("Tada! Simple as that! Enjoy your stuff!");
	close;
	
enchCapricious:

	cleartext;
	if ( !getequipcardcnt(.@part) ) {
		mes("[Serra]");
		mes("This bijou can only work on items that are already enchanted.\n\nAdd some shards to it first, yeah?");
		close;
	}
	
	mes("[Serra]");		
	mes("Now, this Capricious Bijou will ^FF0000^borandomize the types and values^ld^000000 of every enchantment on here.\n\nYou sure you're okay with that?");
	if( select("Sounds good!","I changed my mind.") == 2 ) {
		cleartext;
		mes("[Serra]");
		mes("Ah, alright then.");
		close;
	}	
	
	.@type = getequipid(.@part);
	.@qual = getequiprefinerycnt(.@part);
	.@ilvl = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);

	unequip(.@part);
	delitem2(.@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);
	delitem(.@bijou,1);

	.@ench1 = (.@ench1) ? rand(.@SHARDMIN,.@SHARDMAX) : 0;
	.@ench2 = (.@ench2) ? rand(.@SHARDMIN,.@SHARDMAX) : 0;
	.@ench3 = (.@ench3) ? rand(.@SHARDMIN,.@SHARDMAX) : 0;
	.@ench4 = (.@ench4) ? rand(.@SEALMIN,.@SEALMAX)   : 0;
	
	.@roll1 = (.@ilvl / 2) + rand(24 + .@ilvl);
	.@roll2 = (.@ilvl / 2) + rand(24 + .@ilvl);
	.@roll3 = (.@ilvl / 2) + rand(24 + .@ilvl);
	.@roll4 = (.@ilvl / 2) + rand(24 + .@ilvl);

	autoequip(.@type,1);
	spawnitem(.@type,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@type,0);

	cleartext;
	mes("[Serra]");
	mes("Tada! Simple as that! Enjoy your stuff!");
	close;
	
enchDiscordant:

	cleartext;
	mes("[Serra]");		
	mes("Now, this Discordant Bijou will ^FF0000^borandomize^ld^000000 the enchantments and properties on this item.\n\nDon't blame me if you don't like the result!");
	
	if( select("Sounds good!","I changed my mind.") == 2 ) {
		cleartext;
		mes("[Serra]");
		mes("Ah, alright then.");
		close;
	}	
	
	.@type = getequipid(.@part);
	.@qual = getequiprefinerycnt(.@part);
	.@ilvl = getequipilvl(.@part);
	
	.@ench1 = getequipcardid(.@part,0);
	.@ench2 = getequipcardid(.@part,1);
	.@ench3 = getequipcardid(.@part,2);
	.@ench4 = getequipcardid(.@part,3);

	unequip(.@part);
	delitem2(.@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4);	
	delitem(.@bijou,1);

	.@qual = (rand(4) == 3) ? rand(85,135) : rand(65,110);

	.@ilvl = (rand(0,1)) ? rand(BaseLevel + 1) : BaseLevel + 5 - rand(20);
	.@ilvl = (.@ilvl < 1) ? 1 : .@ilvl;

	.@rand = rand(100);

	if(.@rand  >= 75) {
		.@ench1 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench2 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench3 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench4 = rand(.@SEALMIN,.@SEALMAX);
	} else if(.@rand  >= 50) {
		.@ench1 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench2 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench3 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench4 = 0;
	} else if(.@rand  >= 25) {
		.@ench1 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench2 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench3 = 0;
		.@ench4 = 0;
	} else {
		.@ench1 = rand(.@SHARDMIN,.@SHARDMAX);
		.@ench2 = 0;
		.@ench3 = 0;
		.@ench4 = 0;
	}
	
	.@roll1 = rand(110);
	.@roll2 = rand(110);
	.@roll3 = rand(110);
	.@roll4 = rand(110);

	autoequip(.@type,1);
	spawnitem(.@type,1,.@qual,.@ilvl,1,.@ench1,.@ench2,.@ench3,.@ench4,.@roll1,.@roll2,.@roll3,.@roll4);
	autoequip(.@type,0);

	cleartext;
	mes("[Serra]");
	mes("Tada! Simple as that! Enjoy your stuff!");
	close;

thatsAll:

	cleartext;
	mes("[Serra]");
	mes("See ya later!");
	close;
}